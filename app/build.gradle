plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.android'
}

android {

    compileSdk Integer.parseInt(libs.versions.androidCompileSdkVersion.get())

    namespace 'com.doctoror.particleswallpaper'

    defaultConfig {
        applicationId "com.doctoror.particleswallpaper"

        minSdk Integer.parseInt(libs.versions.androidMinSdkVersion.get())
        targetSdk Integer.parseInt(libs.versions.androidTargetSdkVersion.get())

        versionCode 42
        versionName "2.3.2"

        resConfigs "en", "ru", "uk"
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests.includeAndroidResources true
    }

    signingConfigs {

        release {
            storeFile file("../keystore/upload.jks")
        }
    }

    buildTypes {

        debug {
        }

        release {
            debuggable false
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-glide.pro'
            proguardFile 'proguard-lifecycle.pro'
            proguardFile 'proguard-rx-java.pro'
        }
    }

    lintOptions {
        checkAllWarnings true
        ignore "AppCompatResource"
    }
}

dependencies {
    testImplementation libs.test.core
    testImplementation libs.junit.vintage
    testImplementation libs.koin.test
    testImplementation libs.kotlin.reflect
    testImplementation libs.mockito.kotlin
    testImplementation libs.opengl.android
    testImplementation libs.robolectric

    implementation libs.koin.android
    implementation libs.rxJava
    implementation libs.rxAndroid

    implementation files('libs/library-exposed-release.aar')
    implementation files('libs/opengl-exposed-release.aar')
    implementation libs.color.picker

    implementation libs.androidx.activity
    implementation libs.androidx.annotation
    implementation libs.androidx.lifecycle

    implementation(libs.licensesdialog) {
        exclude(group: 'androidx.appcompat', module: 'appcompat')
        exclude(group: 'com.android.support', module: 'appcompat-v7')
    }

    implementation libs.glide
    ksp libs.glide.ksp
}
