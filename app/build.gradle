apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        applicationId "com.doctoror.particleswallpaper"

        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

        versionCode 26
        versionName "1.3.1"

        resConfigs "en", "ru", "uk"
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.includeAndroidResources true
    }

    signingConfigs {

        release {
            storeFile file("../keystore/upload.jks")
        }
    }

    flavorDimensions 'default'

    productFlavors {
        canvas {
            buildConfigField 'boolean', 'OPEN_GL_VARIANT', 'false'
        }

        opengl {
            buildConfigField 'boolean', 'OPEN_GL_VARIANT', 'true'
        }
    }

    buildTypes {

        debug {
        }

        release {
            debuggable false
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-glide.pro'
            proguardFile 'proguard-lifecycle.pro'
            proguardFile 'proguard-rx-java.pro'
        }
    }

    lintOptions {
        checkAllWarnings true
        ignore "AppCompatResource"
    }
}

dependencies {
    def d = rootProject.ext.wallpaperappDependencies
    def td = rootProject.ext.wallpaperappTestDependencies

    testImplementation td.androidxTestCore
    testImplementation td.junitJupiter
    testImplementation td.junitVintage
    testImplementation td.koinTest
    testImplementation td.kotlinReflect
    testImplementation td.robolectric
    testImplementation td.mockitoCore
    testImplementation td.mockitoKotlin

    implementation d.koin
    implementation d.kotlin
    implementation d.rxJava
    implementation d.rxAndroid

    implementation(name: 'library-exposed-release', ext: 'aar')
    implementation(name: 'opengl-exposed-release', ext: 'aar')
    implementation d.colorPicker

    implementation d.androidxAnnotations
    implementation d.androidxCore

    implementation(d.licencesDialog) {
        exclude(group: 'androidx.appcompat', module: 'appcompat')
        exclude(group: 'com.android.support', module: 'appcompat-v7')
    }

    implementation d.glide
    kapt d.glideCompiler
}

repositories {
    mavenCentral()
}
