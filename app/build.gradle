apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        applicationId "com.doctoror.particleswallpaper"

        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

        versionCode 19
        versionName "1.3.0"

        resConfigs "en", "ru", "uk"
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.includeAndroidResources true
    }

    signingConfigs {

        release {
            storeFile file("../keystore/upload.jks")
        }
    }

    buildTypes {

        debug {
        }

        release {
            debuggable false
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-dagger.pro'
            proguardFile 'proguard-glide.pro'
            proguardFile 'proguard-lifecycle.pro'
            proguardFile 'proguard-rx-java.pro'
            proguardFile 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkAllWarnings true
        ignore "AppCompatResource"
    }
}

dependencies {
    def d = rootProject.ext.wallpaperappDependencies
    def td = rootProject.ext.wallpaperappTestDependencies

    testImplementation td.junitJupiter
    testImplementation td.junitVintage
    testImplementation td.kotlinReflect
    testImplementation td.robolectric
    testImplementation td.mockitoCore
    testImplementation td.mockitoKotlin

    implementation d.kotlin
    implementation d.rxJava
    implementation d.rxAndroid

    implementation d.particlesDrawable
    implementation d.colorPicker

    implementation d.supportV4
    implementation d.supportAnnotations

    implementation(d.licencesDialog) {
        exclude(group: 'com.android.support', module: 'appcompat-v7')
    }

    implementation d.glide
    kapt d.glideCompiler

    implementation d.gendalf
    kapt d.gendalfCompiler

    implementation d.dagger
    implementation d.daggerAndroid
    kapt d.daggerAndroidProcessor
    kapt d.daggerCompiler
}

repositories {
    mavenCentral()
}
